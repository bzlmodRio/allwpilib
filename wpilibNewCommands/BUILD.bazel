load("@allwpilib_pip_deps//:requirements.bzl", "requirement")
load("@aspect_bazel_lib//lib:write_source_files.bzl", "write_source_files")
load("@rules_python//python:defs.bzl", "py_binary")
load("//shared/bazel/rules:cc_rules.bzl", "wpilib_cc_binary", "wpilib_cc_library", "wpilib_cc_static_and_shared_library", "wpilib_cc_test")
load("//shared/bazel/rules:java_rules.bzl", "wpilib_java_binary", "wpilib_java_junit5_test", "wpilib_java_library")
load("//shared/bazel/rules:publishing.bzl", "assemble_cpp_library", "assemble_java_library", "bundle_artifacts")
load("//wpilibNewCommands:generate.bzl", "generate_wpilib_new_commands")

py_binary(
    name = "generate_hids",
    srcs = ["generate_hids.py"],
    tags = ["manual"],
    visibility = ["//visibility:public"],
    deps = [requirement("jinja2")],
)

filegroup(
    name = "templates",
    srcs = glob(["src/generate/main/**"]) + [
        "//wpilibj:hid_schema",
    ],
    visibility = ["//wpilibNewCommands:__subpackages__"],
)

generate_wpilib_new_commands(
    name = "generate_wpilib_new_commands",
)

write_source_files(
    name = "write_wpilib_new_commands",
    files = {
        "src/generated": ":generate_wpilib_new_commands",
    },
    suggested_update_target = "//:write_all",
    visibility = ["//visibility:public"],
)

wpilib_cc_library(
    name = "generated_cc_headers",
    hdrs = glob(["src/generated/main/native/include/**"]),
    includes = ["src/generated/main/native/include"],
    strip_include_prefix = "src/generated/main/native/include",
    visibility = ["//wpilibNewCommands:__subpackages__"],
)

filegroup(
    name = "generated_cc_source",
    srcs = glob(["src/generated/main/native/cpp/**"]),
    visibility = ["//wpilibNewCommands:__subpackages__"],
)

filegroup(
    name = "generated_java",
    srcs = glob(["src/generated/main/java/**/*.java"]),
    visibility = ["//wpilibNewCommands:__subpackages__"],
)

wpilib_cc_static_and_shared_library(
    name = "wpilibNewCommands",
    srcs = glob(["src/main/native/cpp/**"]) + ["//wpilibNewCommands:generated_cc_source"],
    hdrs = glob(["src/main/native/include/**"]),
    includes = ["src/main/native/include"],
    standard_deps = [
        ":generated_cc_headers",
    ],
    strip_include_prefix = "src/main/native/include",
    visibility = ["//visibility:public"],
    wpi_maybe_shared_deps = [
        "//wpilibc:wpilibc",
    ],
)

wpilib_java_library(
    name = "wpilibNewCommands-java",
    srcs = glob(["src/main/java/**/*.java"]) + ["//wpilibNewCommands:generated_java"],
    visibility = ["//visibility:public"],
    deps = [
        "//cscore:cscore-java",
        "//hal:hal-java",
        "//ntcore:ntcore-java",
        "//wpilibj",
        "//wpimath:wpimath-java",
        "//wpinet:wpinet-java",
        "//wpiunits",
        "//wpiutil:wpiutil-java",
    ],
)

wpilib_cc_test(
    name = "wpilibNewCommands-cpp-test",
    size = "small",
    srcs = glob([
        "src/test/native/**/*.cpp",
        "src/test/native/**/*.h",
    ]),
    tags = [
        "no-tsan",
        "no-ubsan",
    ],
    deps = [
        ":wpilibNewCommands.static",
        "//thirdparty/googletest:googletest.static",
    ],
)

wpilib_java_junit5_test(
    name = "wpilibNewCommands-java-test",
    size = "small",
    srcs = glob(["**/*.java"]),
    resource_strip_prefix = "wpilibNewCommands/src/test/resources",
    resources = glob(["src/test/resources/**"]),
    deps = [
        "//hal:hal-java",
        "//ntcore:ntcore-java",
        "//wpilibNewCommands",
        "//wpilibj",
        "//wpimath:wpimath-java",
        "//wpiunits",
        "//wpiutil:wpiutil-java",
        "@maven//:org_mockito_mockito_core",
    ],
)

wpilib_cc_binary(
    name = "DevMain-Cpp",
    srcs = ["src/dev/native/cpp/main.cpp"],
    deps = [
    ],
)

wpilib_java_binary(
    name = "DevMain-Java",
    srcs = ["src/dev/java/edu/wpi/first/wpilibj2/commands/DevMain.java"],
    main_class = "edu.wpi.first.wpilibj2.commands.DevMain",
    deps = [
        "//hal:hal-java",
        "//ntcore:ntcore-java",
        "//wpimath:wpimath-java",
        "//wpiutil:wpiutil-java",
    ],
)

assemble_cpp_library(
    base_name = "wpilibNewCommands",
    hdr_directories = [
        "src/main/native/include",
        "src/generated/main/native/include",
    ],
    shared_library_pkg_files = "wpilibNewCommands.shared_pkg_files",
    src_directories = ["src/main/native/cpp"],
    static_library_pkg_files = "wpilibNewCommands.static_pkg_files",
)

assemble_java_library(
    base_name = "wpilibNewCommands",
    src_directories = [
        "src/main/java",
        "src/generated/main/java",
    ],
)

bundle_artifacts(
    name = "wpilibNewCommands-publishing",
    artifacts = [
        ("edu.wpi.first.wpilibNewCommands:wpilibNewCommands-cpp:-headers", "wpilibNewCommands.hdrs_pkg"),
        ("edu.wpi.first.wpilibNewCommands:wpilibNewCommands-cpp:-sources", "wpilibNewCommands.srcs_pkg"),
        ("edu.wpi.first.wpilibNewCommands:wpilibNewCommands-cpp:-{platform}", "wpilibNewCommands.shared_pkg"),
        ("edu.wpi.first.wpilibNewCommands:wpilibNewCommands-cpp:-{platform}static", "wpilibNewCommands.static_pkg"),
        ("edu.wpi.first.wpilibNewCommands:wpilibNewCommands-java:-sources", "wpilibNewCommands.java_srcs_pkg"),
        # ("edu.wpi.first.wpilibNewCommands:wpilibNewCommands-java:-sources", "libwpilibNewCommands-java-src.jar"),
    ],
)
