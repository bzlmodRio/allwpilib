load("@rules_pkg//:mappings.bzl", "pkg_files")
load("//shared/bazel/rules:cc_rules.bzl", "wpilib_cc_library")
load("//shared/bazel/rules:objectivec_rules.bzl", "wpilib_objc_library")
load("//shared/bazel/rules:publishing.bzl", "assemble_cpp_library", "bundle_artifacts")

WIN_SRCS = glob(["src/main/native/directx11/**/*.cpp"])

LINUX_SRCS = glob(["src/main/native/opengl3/**/*.cpp"])

MAC_SRCS = glob(["src/main/native/metal/**/*.mm"])

wpilib_cc_library(
    name = "headers",
    hdrs = glob(["src/main/native/include/**/*"]),
    strip_include_prefix = "src/main/native/include",
)

wpilib_objc_library(
    name = "wpigui-mac",
    srcs = MAC_SRCS,
    deps = [
        ":headers",
        "//thirdparty/imgui_suite",
    ],
)

wpilib_cc_library(
    name = "wpigui",
    srcs = glob(["src/main/native/cpp/**/*.cpp"]) +
           select({
               "@bazel_tools//src/conditions:darwin": [],
               "@bazel_tools//src/conditions:windows": WIN_SRCS,
               "@rules_bzlmodrio_toolchains//constraints/combined:is_linux": LINUX_SRCS,
           }),
    strip_include_prefix = "include",
    tags = [
        "no-roborio",
        "wpi-cpp-gui",
    ],
    visibility = ["//visibility:public"],
    deps = [
        ":headers",
        "//thirdparty/imgui_suite",
    ] + select({
        "@bazel_tools//src/conditions:darwin": [":wpigui-mac"],
        "//conditions:default": [],
    }),
)

pkg_files(
    name = "wpigui.static_pkg_files",
    srcs = ["wpigui"],
    excludes = ["**/*.so"],
    prefix = "linux/x86-64/static",
)

assemble_cpp_library(
    base_name = "wpigui",
    hdr_directories = ["src/main/native/include"],
    shared_library_pkg_files = None,
    src_directories = ["src/main/native/cpp"],
    static_library_pkg_files = "wpigui.static_pkg_files",
    include_license_files_in_srcs = True,
)

bundle_artifacts(
    name = "wpigui-publishing",
    artifacts = [
        ("edu.wpi.first.wpigui:wpigui-cpp:-headers", "wpigui.hdrs_pkg"),
        ("edu.wpi.first.wpigui:wpigui-cpp:-sources", "wpigui.srcs_pkg"),
        ("edu.wpi.first.wpigui:wpigui-cpp:-{platform}static", "wpigui.static_pkg"),
    ],
)
