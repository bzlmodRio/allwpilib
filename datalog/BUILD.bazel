load("@rules_cc//cc:defs.bzl", "cc_binary", "cc_test")
load("@rules_java//java:defs.bzl", "java_binary")
load("@rules_python//python:defs.bzl", "py_binary")
load("//shared/bazel/rules:cc_rules.bzl", "wpilib_cc_library", "wpilib_cc_shared_library", "wpilib_cc_static_library")
load("//shared/bazel/rules:java_rules.bzl", "wpilib_java_junit5_test")
load("//shared/bazel/rules:jni_rules.bzl", "wpilib_jni_cc_library", "wpilib_jni_java_library")

wpilib_cc_library(
    name = "datalog_base",
    srcs = glob(
        ["src/main/native/cpp/**"],
        exclude = ["src/main/native/cpp/jni/**"],
    ),
    hdrs = glob(["src/main/native/include/**"]),
    extra_src_pkg_files = [":datalog-java-jni-hdrs-pkg"],
    includes = [
        "src/main/native/cpp",
        "src/main/native/include",
    ],
    strip_include_prefix = "src/main/native/include",
    visibility = ["//visibility:public"],
    deps = [
        "//wpiutil:wpiutil_base",
    ],
)

wpilib_cc_shared_library(
    name = "datalog",
    dynamic_deps = [
        "//wpiutil:shared/wpiutil",
    ],
    deps = [":datalog_base"],
)

alias(
    name = "shared/datalog",
    actual = ":datalog",
    visibility = ["//visibility:public"],
)

wpilib_cc_static_library(
    name = "static/datalog",
    static_deps = [
        "//wpiutil:static/wpiutil",
    ],
    visibility = ["//visibility:public"],
    deps = [":datalog_base"],
)

wpilib_jni_cc_library(
    name = "datalogjni",
    srcs = glob(["src/main/native/cpp/jni/**"]),
    java_dep = ":datalog-java",
    visibility = ["//visibility:public"],
    deps = [
        ":datalog_base",
    ],
)

wpilib_cc_shared_library(
    name = "shared/datalogjni",
    auto_export_windows_symbols = False,
    dynamic_deps = [
        ":shared/datalog",
        "//wpiutil:shared/wpiutil",
    ],
    visibility = ["//visibility:public"],
    deps = [":datalogjni"],
)

wpilib_jni_java_library(
    name = "datalog-java",
    srcs = glob(["src/main/java/**/*.java"]),
    native_libs = [":datalogjni"],
    visibility = ["//visibility:public"],
    deps = [
        "//wpiutil:wpiutil-java",
        "@maven//:us_hebi_quickbuf_quickbuf_runtime",
    ],
)

py_binary(
    name = "datalog_example",
    srcs = ["examples/printlog/datalog.py"],
    main = "examples/printlog/datalog.py",
    target_compatible_with = select({
        "@rules_bzlmodrio_toolchains//constraints/is_systemcore:systemcore": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),
)

cc_binary(
    name = "printlog",
    srcs = ["examples/printlog/printlog.cpp"],
    deps = [
        ":datalog_base",
        "//wpiutil:wpiutil_base",
    ],
)

cc_binary(
    name = "writelog",
    srcs = ["examples/writelog/writelog.cpp"],
    deps = [
        ":datalog_base",
        "//wpiutil:wpiutil_base",
    ],
)

java_binary(
    name = "printlog-java",
    srcs = ["src/printlog/java/printlog/PrintLog.java"],
    main_class = "printlog.PrintLog",
    deps = [
        ":datalog-java",
    ],
)

cc_test(
    name = "datalog-cpp-test",
    size = "small",
    srcs = glob(["src/test/native/**/*.cpp"]),
    tags = [
        "exclusive",
        "no-asan",
        "no-tsan",
    ],
    deps = [
        ":datalog_base",
        "//thirdparty/googletest",
        "//wpiutil:wpiutil-testlib",
    ],
    dynamic_deps = [
        ":shared/datalog",
    ],
)

wpilib_java_junit5_test(
    name = "datalog-java-test",
    srcs = glob(["src/test/java/**/*.java"]),
    tags = ["exclusive"],
    deps = [
        ":datalog-java",
        "//wpiutil:wpiutil-java",
    ],
)
