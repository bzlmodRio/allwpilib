load("@allwpilib_pip_deps//:requirements.bzl", "requirement")
load("@aspect_bazel_lib//lib:write_source_files.bzl", "write_source_files")
load("@rules_python//python:defs.bzl", "py_binary")
load("//ntcore:generate_ntcore.bzl", "generate_ntcore")
load("//shared/bazel/rules:cc_rules.bzl", "wpilib_cc_binary", "wpilib_cc_library", "wpilib_cc_static_and_shared_library", "wpilib_cc_test")
load("//shared/bazel/rules:java_rules.bzl", "wpilib_java_binary", "wpilib_java_junit5_test")
load("//shared/bazel/rules:jni_rules.bzl", "wpilib_jni_cc_library", "wpilib_jni_java_library")

generate_ntcore(
    name = "generate_ntcore",
)

filegroup(
    name = "templates",
    srcs = glob([
        "src/generate/**/*.jinja",
        "src/generate/*.json",
    ]),
)

py_binary(
    name = "generate_topics",
    srcs = ["generate_topics.py"],
    data = [":templates"],
    target_compatible_with = select({
        "@rules_bzlmodrio_toolchains//constraints/is_roborio:roborio": ["@platforms//:incompatible"],
        "@rules_bzlmodrio_toolchains//constraints/is_systemcore:systemcore": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),
    visibility = ["//visibility:public"],
    deps = [requirement("jinja2")],
)

write_source_files(
    name = "write_ntcore",
    files = {
        "src/generated": ":generate_ntcore",
    },
    suggested_update_target = "//:write_all",
    tags = ["pregeneration"],
    visibility = ["//visibility:public"],
)

wpilib_cc_library(
    name = "generated_cc_headers",
    hdrs = glob(["src/generated/main/native/include/**"]),
    includes = ["src/generated/main/native/include"],
    strip_include_prefix = "src/generated/main/native/include",
    visibility = ["//ntcore:__subpackages__"],
)

filegroup(
    name = "generated_cc_source",
    srcs = glob(
        ["src/generated/main/native/cpp/**"],
        exclude = ["src/generated/main/native/cpp/jni/**"],
    ),
    visibility = ["//ntcore:__subpackages__"],
)

filegroup(
    name = "generated_jni",
    srcs = glob(["src/generated/main/native/cpp/jni/**"]),
    visibility = ["//ntcore:__subpackages__"],
)

filegroup(
    name = "generated_java",
    srcs = glob(["src/generated/main/java/**/*.java"]),
    visibility = ["//ntcore:__subpackages__"],
)

wpilib_cc_static_and_shared_library(
    name = "ntcore",
    srcs = glob(
        ["src/main/native/cpp/**"],
        exclude = ["src/main/native/cpp/jni/**"],
    ) + [":generated_cc_source"],
    hdrs = glob(["src/main/native/include/**/*"]),
    includes = [
        "src/main/native/cpp",
        "src/main/native/include",
    ],
    standard_deps = [
        ":generated_cc_headers",
    ],
    strip_include_prefix = "src/main/native/include",
    visibility = ["//visibility:public"],
    wpi_maybe_shared_deps = [
        "//datalog:datalog",
        "//wpinet:wpinet",
        "//wpiutil:wpiutil",
    ],
)

wpilib_jni_cc_library(
    name = "ntcorejni",
    srcs = glob(["src/main/native/cpp/jni/**"]) + [":generated_jni"],
    java_dep = ":networktables-java",
    visibility = ["//visibility:public"],
    wpi_maybe_shared_deps = [
        ":ntcore",
    ],
)

wpilib_jni_java_library(
    name = "networktables-java",
    srcs = glob(["src/main/java/**/*.java"]) + [":generated_java"],
    native_libs = [":ntcorejni"],
    visibility = ["//visibility:public"],
    deps = [
        "//datalog:datalog-java",
        "//wpiutil:wpiutil-java",
        "@maven//:us_hebi_quickbuf_quickbuf_runtime",
    ],
)

wpilib_cc_test(
    name = "ntcore-cpp-test",
    size = "small",
    srcs = glob([
        "src/test/native/**/*.cpp",
        "src/test/native/**/*.h",
    ]),
    tags = [
        "exclusive",
        "no-asan",
        "no-tsan",
    ],
    deps = [
        ":ntcore.static",
        "//thirdparty/googletest:googletest.static",
        "//wpiutil:wpiutil-testlib",
    ],
)

wpilib_java_junit5_test(
    name = "ntcore-java-test",
    size = "small",
    srcs = glob(["src/test/java/**/*.java"]),
    tags = ["exclusive"],
    deps = [
        ":networktables-java",
        "//wpiutil:wpiutil-java",
    ],
)

wpilib_cc_binary(
    name = "DevMain-Cpp",
    srcs = ["src/dev/native/cpp/main.cpp"],
    deps = [
        ":ntcore.shared",
    ],
)

wpilib_java_binary(
    name = "DevMain-Java",
    srcs = ["src/dev/java/edu/wpi/first/ntcore/DevMain.java"],
    main_class = "edu.wpi.first.ntcore.DevMain",
    deps = [
        "networktables-java",
        "//wpiutil:wpiutil-java",
    ],
)
