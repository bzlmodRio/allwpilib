load("@rules_cc//cc:defs.bzl", "cc_binary")
load("@rules_java//java:defs.bzl", "java_binary")
load("@rules_python//python:defs.bzl", "py_binary")
load("//shared/bazel/rules:cc_rules.bzl", "wpilib_cc_binary", "wpilib_cc_library", "wpilib_cc_static_and_shared_library", "wpilib_cc_test")
load("//shared/bazel/rules:java_rules.bzl", "wpilib_java_binary", "wpilib_java_junit5_test")
load("//shared/bazel/rules/gen:gen-resources.bzl", "generate_resources")
load("//shared/bazel/rules/jni:cc_jni.bzl", "wpilib_jni_cc_library")
load("//shared/bazel/rules/jni:java_jni.bzl", "wpilib_java_jni_library")

wpilib_cc_library(
    name = "expected-headers",
    hdrs = glob([
        "src/main/native/thirdparty/expected/include/**/*",
    ]),
    includes = ["src/main/native/thirdparty/expected/include"],
    strip_include_prefix = "src/main/native/thirdparty/expected/include",
    visibility = ["//wpiutil:__subpackages__"],
)

wpilib_cc_library(
    name = "fmtlib-headers",
    hdrs = glob([
        "src/main/native/thirdparty/fmtlib/include/**/*.h",
    ]),
    includes = ["src/main/native/thirdparty/fmtlib/include"],
    strip_include_prefix = "src/main/native/thirdparty/fmtlib/include",
    visibility = ["//wpiutil:__subpackages__"],
)

filegroup(
    name = "fmtlib-srcs",
    srcs = glob(["src/main/native/thirdparty/fmtlib/src/**"]),
    visibility = ["//wpiutil:__subpackages__"],
)

wpilib_cc_library(
    name = "json-headers",
    hdrs = glob([
        "src/main/native/thirdparty/json/include/**/*.h",
    ]),
    includes = ["src/main/native/thirdparty/json/include"],
    strip_include_prefix = "src/main/native/thirdparty/json/include",
    visibility = ["//wpiutil:__subpackages__"],
)

wpilib_cc_library(
    name = "llvm-headers",
    hdrs = glob([
        "src/main/native/thirdparty/llvm/include/**/*.h",
    ]),
    includes = ["src/main/native/thirdparty/llvm/include"],
    strip_include_prefix = "src/main/native/thirdparty/llvm/include",
    visibility = ["//wpiutil:__subpackages__"],
)

filegroup(
    name = "llvm-srcs",
    srcs = glob(["src/main/native/thirdparty/llvm/cpp/**"]),
    visibility = ["//wpiutil:__subpackages__"],
)

wpilib_cc_library(
    name = "memory-headers",
    hdrs = glob([
        "src/main/native/thirdparty/memory/include/**/*.hpp",
    ]),
    includes = ["src/main/native/thirdparty/memory/include"],
    strip_include_prefix = "src/main/native/thirdparty/memory/include",
    visibility = ["//wpiutil:__subpackages__"],
)

filegroup(
    name = "memory-srcs",
    srcs = glob(["src/main/native/thirdparty/memory/src/**"]),
    visibility = ["//wpiutil:__subpackages__"],
)

wpilib_cc_library(
    name = "mpack-headers",
    hdrs = glob([
        "src/main/native/thirdparty/mpack/include/**/*.h",
    ]),
    includes = ["src/main/native/thirdparty/mpack/include"],
    strip_include_prefix = "src/main/native/thirdparty/mpack/include",
    visibility = ["//wpiutil:__subpackages__"],
)

filegroup(
    name = "mpack-srcs",
    srcs = glob(["src/main/native/thirdparty/mpack/src/**"]),
    visibility = ["//wpiutil:__subpackages__"],
)

wpilib_cc_library(
    name = "protobuf-headers",
    hdrs = glob([
        "src/main/native/thirdparty/protobuf/include/**/*.h",
        "src/main/native/thirdparty/protobuf/include/**/*.inc",
    ]),
    includes = ["src/main/native/thirdparty/protobuf/include"],
    strip_include_prefix = "src/main/native/thirdparty/protobuf/include",
    visibility = ["//wpiutil:__subpackages__"],
)

filegroup(
    name = "protobuf-srcs",
    srcs = glob(["src/main/native/thirdparty/protobuf/src/**"]),
    visibility = ["//wpiutil:__subpackages__"],
)

wpilib_cc_library(
    name = "sigslot-headers",
    hdrs = glob([
        "src/main/native/thirdparty/sigslot/include/**/*.h",
    ]),
    includes = ["src/main/native/thirdparty/sigslot/include"],
    strip_include_prefix = "src/main/native/thirdparty/sigslot/include",
    visibility = ["//wpiutil:__subpackages__"],
)

generate_resources(
    name = "generate-resources",
    namespace = "dlt",
    prefix = "DLT",
    resource_files = glob(["src/main/native/resources/*"]),
)

filegroup(
    name = "native-srcs",
    srcs = select({
        "@bazel_tools//src/conditions:windows": glob([
            "src/main/native/windows/*.cpp",
            "src/main/native/windows/*.h",
        ]),
        "@rules_bzlmodrio_toolchains//constraints/combined:is_unix": glob(["src/main/native/unix/*.cpp"]),
    }),
)

wpilib_cc_static_and_shared_library(
    name = "wpiutil",
    srcs = glob(
        ["src/main/native/cpp/**"],
        exclude = ["src/main/native/cpp/jni/**"],
    ) + [
        ":fmtlib-srcs",
        ":generate-resources",
        ":llvm-srcs",
        ":memory-srcs",
        ":mpack-srcs",
        ":native-srcs",
        ":protobuf-srcs",
    ],
    hdrs = glob(["src/main/native/include/**/*"]),
    includes = ["src/main/native/include"],
    standard_deps = [
        ":expected-headers",
        ":fmtlib-headers",
        ":json-headers",
        ":llvm-headers",
        ":memory-headers",
        ":mpack-headers",
        ":protobuf-headers",
        ":sigslot-headers",
    ] + select({
        "@rules_bzlmodrio_toolchains//constraints/is_roborio:roborio": ["@bzlmodrio-ni//libraries/cpp/ni:shared"],
        "//conditions:default": [],
    }),
    strip_include_prefix = "src/main/native/include",
    visibility = ["//visibility:public"],
)

wpilib_jni_cc_library(
    name = "wpiutiljni",
    srcs = glob(["src/main/native/cpp/jni/**"]),
    java_dep = ":wpiutil-java",
    visibility = ["//visibility:public"],
    wpi_maybe_shared_deps = [
        ":wpiutil",
    ],
)

wpilib_java_jni_library(
    name = "wpiutil-java",
    srcs = glob(["src/main/java/**/*.java"]),
    native_libs = [":wpiutiljni"],
    visibility = ["//visibility:public"],
    deps = [
        "@maven//:com_fasterxml_jackson_core_jackson_annotations",
        "@maven//:com_fasterxml_jackson_core_jackson_core",
        "@maven//:com_fasterxml_jackson_core_jackson_databind",
        "@maven//:us_hebi_quickbuf_quickbuf_runtime",
    ],
)

wpilib_cc_library(
    name = "wpiutil-testlib",
    hdrs = glob(["src/test/native/include/**"]),
    strip_include_prefix = "src/test/native/include",
    visibility = ["//visibility:public"],
)

wpilib_cc_test(
    name = "wpiutil-cpp-test",
    size = "small",
    srcs = glob(["src/test/native/cpp/**"]),
    linkstatic = True,
    tags = ["no-tsan"],  # TODO(pj.reiniger) Find problem
    deps = [
        ":wpiutil.static",
        ":wpiutil-testlib",
        "//thirdparty/googletest:googletest.static",
    ],
)

wpilib_java_junit5_test(
    name = "wpiutil-java-test",
    size = "small",
    srcs = glob(["src/test/java/**/*.java"]),
    deps = [
        ":wpiutil-java",
    ],
)

wpilib_cc_binary(
    name = "DevMain-Cpp",
    srcs = ["src/dev/native/cpp/main.cpp"],
    deps = [
        ":wpiutil.shared",
    ],
)

wpilib_java_binary(
    name = "DevMain-Java",
    srcs = ["src/dev/java/edu/wpi/first/util/DevMain.java"],
    main_class = "edu.wpi.first.util.DevMain",
    deps = [
        ":wpiutil-java",
    ],
)

py_binary(
    name = "datalog",
    srcs = ["datalog.py"],
    tags = ["manual"],
)

cc_binary(
    name = "printlog",
    srcs = ["examples/printlog/printlog.cpp"],
    deps = [
        ":wpiutil.static",
    ],
)

cc_binary(
    name = "writelog",
    srcs = ["examples/writelog/writelog.cpp"],
    deps = [
        ":wpiutil.static",
    ],
)

java_binary(
    name = "printlog-java",
    srcs = ["src/printlog/java/printlog/PrintLog.java"],
    main_class = "printlog.PrintLog",
    deps = [
        ":wpiutil-java",
    ],
)
