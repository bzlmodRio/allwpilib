load("@aspect_bazel_lib//lib:write_source_files.bzl", "write_source_files")
load("@rules_cc//cc:defs.bzl", "cc_binary", "cc_library", "cc_test")
load("@rules_java//java:defs.bzl", "java_binary")
load("@rules_pkg//:mappings.bzl", "pkg_files")
load("@rules_python//python:defs.bzl", "py_binary")
load("//hal:generate.bzl", "generate_hal")
load("//shared/bazel/rules:cc_rules.bzl", "wpilib_cc_library")
load("//shared/bazel/rules:java_rules.bzl", "wpilib_java_junit5_test")
load("//shared/bazel/rules:jni_rules.bzl", "wpilib_jni_cc_library", "wpilib_jni_java_library")
load("//shared/bazel/rules/publishing:publishing.bzl", "bundle_library_artifacts")

filegroup(
    name = "templates",
    srcs = glob(["src/generate/*"]),
)

filegroup(
    name = "proto_files",
    srcs = glob(["src/mrc/proto/**/*.proto"]),
)

py_binary(
    name = "generate_nanopb",
    srcs = ["generate_nanopb.py"],
    target_compatible_with = select({
        "@rules_bzlmodrio_toolchains//constraints/is_roborio:roborio": ["@platforms//:incompatible"],
        "@rules_bzlmodrio_toolchains//constraints/is_systemcore:systemcore": ["@platforms//:incompatible"],
        "//conditions:default": [],
    }),
    visibility = ["//visibility:public"],
)

generate_hal(
    name = "generate_hal_files",
    proto_files = ":proto_files",
)

write_source_files(
    name = "write_hal",
    files = {
        "src/generated": ":generate_hal_files",
    },
    suggested_update_target = "//:write_all",
    tags = ["pregeneration"],
    visibility = ["//visibility:public"],
)

cc_library(
    name = "generated_cc_headers",
    hdrs = glob(["src/generated/main/native/include/**"]),
    includes = ["src/generated/main/native/include"],
    strip_include_prefix = "src/generated/main/native/include",
    visibility = ["//hal:__subpackages__"],
)

cc_library(
    name = "mrc_cc_headers",
    hdrs = glob(["src/mrc/include/**"]),
    includes = ["src/mrc/include"],
    strip_include_prefix = "src/mrc/include",
    visibility = ["//hal:__subpackages__"],
)

cc_library(
    name = "generated_mrc_cc_headers",
    hdrs = glob(["src/generated/main/native/cpp/mrc/protobuf/**"]),
    includes = ["src/generated/main/native/cpp/mrc/protobuf"],
    strip_include_prefix = "src/generated/main/native/cpp/mrc/protobuf",
    visibility = ["//hal:__subpackages__"],
)

filegroup(
    name = "generated_java",
    srcs = glob(["src/generated/main/java/**/*.java"]),
    visibility = ["//hal:__subpackages__"],
)

SYSTEMCORE_SRCS = glob(["src/main/native/systemcore/**"])

SIM_SRCS = glob(["src/main/native/sim/**"])

filegroup(
    name = "platform-srcs",
    srcs = select({
        "@rules_bzlmodrio_toolchains//constraints/is_systemcore:systemcore": SYSTEMCORE_SRCS,
        "//conditions:default": SIM_SRCS,
    }),
)

pkg_files(
    name = "hal-sim-pkg",
    srcs = glob(["src/main/native/sim/**"]),
    strip_prefix = "src/main/native",
)

pkg_files(
    name = "hal-systemcore-pkg",
    srcs = glob(["src/main/native/systemcore/**"]),
    strip_prefix = "src/main/native",
)

wpilib_cc_library(
    name = "wpiHal.static",
    srcs = [":platform-srcs"] + glob(
        [
            "src/main/native/cpp/**",
            "src/generated/main/native/cpp/**",
        ],
        exclude = ["src/main/native/cpp/jni/**"],
    ),
    hdrs = glob(["src/main/native/include/**/*"]),
    extra_src_pkg_files = [
        "hal-sim-pkg",
        "hal-systemcore-pkg",
    ],
    includes = ["src/main/native/include"],
    strip_include_prefix = "src/main/native/include",
    visibility = ["//visibility:public"],
    deps = [
        ":generated_cc_headers",
        ":generated_mrc_cc_headers",
        ":mrc_cc_headers",
        "//ntcore:ntcore.static",
        "//wpinet:wpinet.static",
        "//wpiutil:wpiutil.static",
    ],
)

wpilib_jni_cc_library(
    name = "wpiHaljni",
    srcs = glob(["src/main/native/cpp/jni/**"]),
    java_dep = ":hal-java",
    visibility = ["//visibility:public"],
    deps = [
        ":wpiHal.static",
    ],
)

wpilib_jni_java_library(
    name = "hal-java",
    srcs = glob(["src/main/java/**/*.java"]),
    native_libs = [":wpiHaljni"],
    visibility = ["//visibility:public"],
    deps = [
        "//ntcore:networktables-java",
        "//wpinet:wpinet-java",
        "//wpiutil:wpiutil-java",
    ],
)

cc_test(
    name = "hal-cpp-test",
    size = "small",
    srcs = glob([
        "src/test/native/**/*.cpp",
        "src/test/native/**/*.h",
    ]),
    deps = [
        ":wpiHal.static",
        "//thirdparty/googletest:googletest.static",
    ],
)

wpilib_java_junit5_test(
    name = "hal-java-test",
    srcs = glob(["src/test/java/**/*.java"]),
    deps = [
        ":hal-java",
    ],
)

cc_binary(
    name = "DevMain-Cpp",
    srcs = ["src/dev/native/cpp/main.cpp"],
    deps = [
        ":wpiHal.static",
    ],
)

java_binary(
    name = "DevMain-Java",
    srcs = ["src/dev/java/edu/wpi/first/hal/DevMain.java"],
    main_class = "edu.wpi.first.hal.DevMain",
    deps = [
    ],
)

bundle_library_artifacts(
    name = "publishing_bundle",
    cc_hdr_pkg = ":wpiHal.static-hdrs-zip",
    cc_src_pkg = ":wpiHal.static-srcs-zip",
    cc_static_library_pkg = ":wpiHal.static-zip",
    group_id = "edu.wpi.first.hal",
    jni_pkg = ":wpiHaljni-zip",
    library_base_name = "hal",
)
